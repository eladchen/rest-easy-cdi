apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'com.example.Main'

ext {
    log4jVersion    = '2.11.1'
    undertowVersion = '2.0.15.Final'
    restEasyVersion = '3.6.2.Final'
    weldVersion     = '3.0.5.Final'
    junitVersion    = '5.3.1'
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        output.resourcesDir = 'build/out'
        output.classesDir   = 'build/out'
    }
}

configurations {
    implementation {
        // http://logging.apache.org/log4j/2.x/faq.html#which_jars
        exclude group: 'log4j',                    module: 'log4j'
        exclude group: 'org.slf4j',                module: 'log4j-over-slf4j'
        exclude group: 'ch.qos.logback',           module: 'logback-core'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }
}

dependencies {
    // Embedded Web Server / Container
    implementation group: 'io.undertow', name: 'undertow-core',    version: "$undertowVersion"
    implementation group: 'io.undertow', name: 'undertow-servlet', version: "$undertowVersion"

    // CDI (Weld as the JSR-299 spec)
    implementation group: 'org.jboss',              name: 'jandex',            version: '2.1.0.Beta1'
    implementation group: 'org.jboss.weld.servlet', name: 'weld-servlet-core', version: "$weldVersion"

    // Logging
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api',          version: "$log4jVersion"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core',         version: "$log4jVersion"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j18-impl', version: "$log4jVersion"

    // Restful (RestEasy as the JAX-RS spec & its extensions)
    implementation group: 'org.jboss.resteasy', name: 'resteasy-cdi',                 version: "$restEasyVersion"
    implementation group: 'org.jboss.resteasy', name: 'resteasy-jaxrs',               version: "$restEasyVersion"
    implementation group: 'org.jboss.resteasy', name: 'resteasy-jackson-provider',    version: "$restEasyVersion"

    // Testing
    testImplementation group: 'com.squareup.okhttp3', name: 'okhttp',               version: '3.11.0'
    testImplementation group: 'org.junit.jupiter',    name: 'junit-jupiter-api',    version: "$junitVersion"
    testRuntimeOnly    group: 'org.junit.jupiter',    name: 'junit-jupiter-engine', version: "$junitVersion"
}