apply plugin: "java"
apply plugin: "application"
apply plugin: "idea"

project.sourceCompatibility = JavaVersion.VERSION_1_8
project.targetCompatibility = JavaVersion.VERSION_1_8

ext {
	versions = [
		log4j: 		"2.11.1",
		undertow: "2.0.15.Final",
		restEasy: "3.6.2.Final",
		weld: 		"3.0.5.Final",
		junit: 		"5.3.1",
	]
}

application {
	mainClassName = "com.example.Main"
}

repositories {
	mavenCentral()
}

configurations {
	implementation {
		// http://logging.apache.org/log4j/2.x/faq.html#which_jars
		exclude group: "log4j", module: "log4j"
		exclude group: "org.slf4j", module: "log4j-over-slf4j"
		exclude group: "ch.qos.logback", module: "logback-core"
		exclude group: "org.apache.logging.log4j", module: "log4j-to-slf4j"
	}
}

dependencies {
	// Embedded Web Server / Container
	// Exclusion is due to https://issues.jboss.org/browse/UNDERTOW-1355
	implementation group: 'io.undertow', name: 'undertow-servlet', version: "${versions.undertow}", {
		exclude group: 'org.jboss.xnio', module: 'xnio-api'
		exclude group: 'org.jboss.xnio', module: 'xnio-nio'
	}

	implementation group: 'org.jboss.xnio', name: 'xnio-api', version: '3.3.8.Final', {
		exclude group: 'org.jboss.threads', module: 'jboss-threads'
	}

	implementation group: 'org.jboss.xnio',		 name: 'xnio-nio',			version: '3.6.5.Final'
	implementation group: 'org.jboss.threads', name: 'jboss-threads', version: '2.3.2.Final'

	// CDI (Weld as the JSR-365 spec provider: https://jcp.org/en/jsr/detail?id=365)
	implementation group: "org.jboss", 							name: "jandex", 					 version: "2.1.0.Beta1"
	implementation group: "org.jboss.weld.se", 			name: "weld-se-core", 		 version: "$versions.weld"
	implementation group: "org.jboss.weld.servlet", name: "weld-servlet-core", version: "$versions.weld"

	// Logging
	implementation group: "org.apache.logging.log4j", name: "log4j-api",					version: "$versions.log4j"
	implementation group: "org.apache.logging.log4j", name: "log4j-core",					version: "$versions.log4j"
	implementation group: "org.apache.logging.log4j", name: "log4j-slf4j18-impl", version: "$versions.log4j"

	// Restful (RestEasy as the JAX-RS spec provider & its extensions)
	implementation group: "org.jboss.resteasy", name: "resteasy-cdi", 						 version: "$versions.restEasy"
	implementation group: "org.jboss.resteasy", name: "resteasy-jaxrs", 					 version: "$versions.restEasy"
	implementation group: "org.jboss.resteasy", name: "resteasy-jackson-provider", version: "$versions.restEasy"

	// Reflection library
	implementation group: "org.reflections", name: "reflections", version: "0.9.11"

	// Persistence (Hibernate as the JPA spec provider)
	implementation group: "org.hibernate", name: "hibernate-core", version: "5.3.7.Final"

	// An in-memory database
	implementation group: "org.hsqldb",    name: "hsqldb", version: "2.4.1"
	implementation group: "org.liquibase", name: "liquibase-cdi", version: "3.6.2"

	// Testing
	testImplementation group: "com.squareup.okhttp3", name: "okhttp", version: "3.11.0"
	
	testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api", version: "$versions.junit"
	
	testRuntimeOnly group: "org.junit.jupiter", name: "junit-jupiter-engine", version: "$versions.junit"
}

tasks.withType(JavaCompile) {
	options.compilerArgs += "-proc:none"
}

test {
	useJUnitPlatform()
}